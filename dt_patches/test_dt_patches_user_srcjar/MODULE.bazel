"""Bazel module ./test/shell/test_examples.sh tests"""

module(name = "scala3_example")

source_repos = use_extension("//:extensions.bzl", "compiler_source_repos")
use_repo(
    source_repos,
    "scala_compiler",
    "scala_compiler_srcjar",
    "scala_library",
    "scala_reflect",
)

bazel_dep(name = "rules_scala", repo_name = "io_bazel_rules_scala")
local_path_override(
    module_name = "rules_scala",
    path = "../..",
)

scala_config = use_extension(
    "@io_bazel_rules_scala//scala/extensions:config.bzl",
    "scala_config",
)
scala_config.settings(
    enable_compiler_dependency_tracking = True,
)
use_repo(scala_config, "io_bazel_rules_scala_config")

scala_deps = use_extension(
    "@io_bazel_rules_scala//scala/extensions:deps.bzl",
    "scala_deps",
)
scala_deps.settings(
    fetch_sources = True,
    validate_scala_version = False,
)

# The `scala_deps.compiler_srcjar()` tag prevents some of the kinds of errors
# represented in the corresponding `WORKSPACE` file, so we have to force
# different ones. In particular, we can't use unspecified data types or kwargs,
# or Bazel itself will error out.

# Invalid
scala_deps.compiler_srcjar(
    url = "foo",
    urls = ["bar"],
    version = "2.12.11",
)

# Invalid
scala_deps.compiler_srcjar(
    label = "baz",
    url = "foo",
    version = "2.12.12",
)

# Invalid
scala_deps.compiler_srcjar(
    label = "baz",
    urls = ["bar"],
    version = "2.12.13",
)
scala_deps.compiler_srcjar(
    integrity = "sha384-yKJTudaHM2dA+VM//elLxhEfOmyCYRHzbLlQcf5jlrR+G5FEW+fBW/b794mQLMOX",
    urls = ["https://repo1.maven.org/maven2/org/scala-lang/scala-compiler/2.12.14/scala-compiler-2.12.14-sources.jar"],
    version = "2.12.14",
)
scala_deps.compiler_srcjar(
    sha256 = "65f783f1fbef7de661224f607ac07ca03c5d19acfdb7f2234ff8def1e79b5cd8",
    url = "https://repo1.maven.org/maven2/org/scala-lang/scala-compiler/2.12.15/scala-compiler-2.12.15-sources.jar",
    version = "2.12.15",
)
scala_deps.compiler_srcjar(
    label = "@scala_compiler_srcjar//jar:downloaded.jar",
    version = "2.12.16",
)

_SCALA_COMPILER_URL_FORMAT = (
    "https://repo1.maven.org/maven2/org/scala-lang/scala-compiler/" +
    "{version}/scala-compiler-{version}-sources.jar?foo"
)

[
    scala_deps.compiler_srcjar(
        url = _SCALA_COMPILER_URL_FORMAT.format(version = scala_version),
        version = scala_version,
    )
    for scala_version in [
        "2.12.17",
        "2.12.18",
        "2.12.19",
        "2.13.11",
        "2.13.12",
        "2.13.14",
    ]
]

use_repo(
    scala_deps,
    "io_bazel_rules_scala_scala_parser_combinators",
    "io_bazel_rules_scala_scala_xml",
)

register_toolchains("//:dt_scala_toolchain")
